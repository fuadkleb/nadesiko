#----------------------------------------------------------------------
# iタウンページ(Lite)対応のデータ収集ソフト
# 2006/07/25版
#----------------------------------------------------------------------
「■利用者の皆さんへ
これは、iタウンページ(Lite)対応のデータ収集ソフトです。
2006年7月度版に対応しています。将来的なバージョンアップには対応しません。
収集したデータについては、iタウンページの規約に従ってください。」と二択。
もし、それがいいえならば、終わる。
#----------------------------------------------------------------------
＃ブラウザテスト
！タイトル＝「iタウンページ(Liteのみ)」
母艦はタイトル
！「nakolib\iタウンデータ解析.nako」を取り込む

メールリストとはリスト。
メールリストのレイアウトは「左」
メールリストのアイテムは空。

ＳＰとはスプリッタ。
ＳＰのレイアウトは「左」

ＷＥＢブラウザとはブラウザ
ＷＥＢブラウザのレイアウトは「全体」
ＷＥＢブラウザのＵＲＬは、「http://itp.ne.jp/lite/」

「,MM,移動(&M)
,MLINE1,|
,M1,戻る(&B),,,ＷＥＢブラウザの戻る
,M2,進む(&N),,,ＷＥＢブラウザの進む
MM,M5,-
MM,M6,任意のページ,,,任意ページ
MM,M9,iタウンページ,,,タウンページ処理
MM,M7,-
MM,M8,閉じる(&C),,,終わる
,M3,機能
M3,M3A,URL表示,Ctrl+U,,URL表示処理
M3,M3B,収集メールを保存,Ctrl+S,,メール保存処理」の、メニュー一括作成。


下バーとはステータスバー。
下バーはタイトル

ＷＥＢブラウザのクリックした時は
　　下バーは、ＷＥＢブラウザの移動先ＵＲＬ


ＷＥＢブラウザの文書完了した時は
　　/*
　　//メールのみの収集
　　Ｓ＝「」
　　HTML＝ＷＥＢブラウザのテキスト。
　　HTMLを反復
　　　　Ｆ＝それを『[a-zA-Z0-9\-\?\.]+\@[a-zA-Z0-9\-\?\.]+』で正規表現マッチ
　　　　Ｆ＝トリム（Ｆ）
　　　　もし、Ｆ＜＞空ならば
　　　　　　Ｓ＝Ｓ＆Ｆ＆改行
　　Ｓ＝トリム（Ｓ）
　　もし、Ｓ＜＞空ならば
　　　　メールリストのアイテムはメールリストのアイテム＆Ｓ＆改行
　　*/
　　HTML=ＷＥＢブラウザのテキスト
　　ＨＴＭＬで「検索結果」が何文字目
　　もし、それ＝０ならば
　　　　ＢＥＥＰ。
　　　　戻る
　　ＨＴＭＬをページ解析処理
　　メールリストのアイテムはメールリストのアイテム＆結果
　　結果＝「」

ＷＥＢブラウザの完了した時は
　　母艦＝タイトル＆「-」＆ＷＥＢブラウザのタイトル

＊任意ページ
　　「ＵＲＬを入力」と尋ねる。
    ＷＥＢブラウザのＵＲＬは、それ。

＊メール保存処理
　　「.txt」の保存ファイル選択。
　　Ｆ＝それ
　　もし、Ｆ＝空ならば抜ける
　　Ｓ＝メールリストのアイテム。
　　ＳをＦへ保存
　　「｛Ｆ｝へ保存しました。」と言う。
　　「既存のメールをクリアしますか？」と二択。
　　もし、それがはいならば
　　　　メールリストのアイテム＝空。

＊URL表示処理
　　ＷＥＢブラウザのＵＲＬを言う。

＊タウンページ処理
　　ＷＥＢブラウザのＵＲＬは、「http://itp.ne.jp/lite/」

