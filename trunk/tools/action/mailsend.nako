/*
[メールの送信]
以下の項目を設定するとメールが送信できます。
---
メールホストは「mail.xxx.jp」
メール差出人は「xxx@nadesi.com」
メール宛先は「xxx@nadesi.com」
メール件名は「なでしこのテスト」
メール本文は「なでしこからメールです。」
メール送信。
*/
!"action.nako"を取り込む。
#-----------------------------------------------------------------------
# GUI
母艦は「メールの送信」
母艦タイトルは母艦のテキスト
母艦のW=340
母艦のH=300
母艦を中央移動。
#-----------------------------------------------------------------------

「メール認証(Auth-SMTP)が必要ですか？
通常は不要です。」と二択。
もし、それが空ならば終わり。

もし、それがはいならば
　　認証必要処理
違えば
　　認証不要処理
ここまで
終わり。

●認証必要処理
　　「ホスト=mail.xxx.jp
差出人=xxx@xxx.jp
宛先=xxx@xxx.jp
件名=?s
ID=?e
パスワード=?e
オプション=?(LOGIN|CRAM-MD5|PLAIN)」と項目記入。
　　もし、それが空ならば、終わり
　　Ｆはそれ。
    「本文をここに記入」でメモ記入
    「『」を『「』に置換。
    「』」を『」』に置換。
    本文はそれ。
    #---挿入するプログラム
　　「
# サーバーの設定
メールホストは『{F@"ホスト"}』
メールIDは、『{F@"ID"}』
メールパスワードは『{F@"パスワード"}』
メールオプションは『{F@"オプション"}』

# メールの内容について
メール差出人は『{F@"差出人"}』
メール宛先は『{F@"宛先"}』
メール件名は『{F@"件名"}』

# 本文について
メール本文は『{本文}』

# 実際の送信処理
メール送信。
『送信完了』と表示。」をエディタ挿入。

●認証不要処理
　　「ホスト=mail.xxx.jp
差出人=xxx@xxx.jp
宛先=xxx@xxx.jp
件名=?s」と項目記入。
　　もし、それが空ならば、終わり
　　Ｆはそれ。
    「本文をここに記入」でメモ記入
    それを言う
    「『」を『「』に置換。
    「』」を『」』に置換。
    本文はそれ。
    #---挿入するプログラム
　　「
# サーバーの設定
メールホストは『{F@"ホスト"}』

# メールの内容について
メール差出人は『{F@"差出人"}』
メール宛先は『{F@"宛先"}』
メール件名は『{F@"件名"}』

# 本文について
メール本文は『{本文}』

# 実際の送信処理
メール送信。
『送信完了』と表示。」をエディタ挿入。
